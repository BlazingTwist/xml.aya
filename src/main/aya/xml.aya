require stack {stack}

class xml

.{?
    - xml_str : the xml document
    - ignore_whitespace : true = no tokens are constructed for trivial whitespace (pure whitespace between elements)
.}
def xml::loads {xml_str::str ignore_whitespace::num xml,
    xml_str ignore_whitespace :(xml.loads) xml!
}

def xml::__init__ {xml_id::num self,
    xml_id self.:xml_id;
}

.{?
    Remove all non-significant whitespace from the xml document.
    - exclude_elements::list<str> : list of element names that won't be traversed when minifying.
    Returns the minified document as a string.
.}
def xml::minify {exclude_elements::list self : unclosed_tags xml_declaration_open xml_tag_open stack^,
    0 :xml_declaration_open;
    0 :xml_tag_open;
    [] stack! :unclosed_tags;

    self.xml_id :(vtd.get_token_count) .R {i,
        [i self.xml_id i :(vtd.get_token)]
    } O {pair,
        .# skip trivial whitespace
        .# ... could be optimized for large text sections by using :(vtd.fragment_trim_whitespace)
        (pair.[1].type ::character_data =) {self.xml_id pair.[0] :(vtd.token_to_raw_string) .trim E 0 =} & !
    } I {pair : token i,
        pair~ :token; :i;
        {
            (token.type ::dec_attr_name =) {
                xml_declaration_open ! {
                    "<?xml"
                    1 :xml_declaration_open;
                }?
            } (token.type ::dec_attr_val =) {
                xml_declaration_open ! {
                    "<?xml "
                    1 :xml_declaration_open;
                }?
            } {
                xml_declaration_open {
                    "?>"
                    0 :xml_declaration_open;
                }?
            }
        }:?

        [ ::starting_tag ::character_data ::comment ::pi_name ::cdata_val ] token.type H {
            xml_tag_open {
                (token.depth unclosed_tags E <) {unclosed_tags E 0 >} & {
                    .# currently open tag has no content
                    "/>"
                    unclosed_tags.pop;
                }{
                    ">"
                }.?
                0 :xml_tag_open;
            }?

            while {(token.depth unclosed_tags E <) {unclosed_tags E 0 >} &} {
                "</" unclosed_tags.pop ">"
            }
        }?

        {
            (token.type ::starting_tag =) {
                "<"
                self.xml_id i :(vtd.token_to_raw_string)
                    :& unclosed_tags.push;
                1 :xml_tag_open;
                .A""%
            } ([::dec_attr_name ::attr_ns ::attr_name] token.type H) {
                " "
                self.xml_id i :(vtd.token_to_raw_string)
                .A""%
            } ([::dec_attr_val ::attr_val] token.type H) {
                "=\""
                self.xml_id i :(vtd.token_to_raw_string)
                "\""
                .A""%
            } (token.type ::character_data =) {
                self.xml_id i :(vtd.token_to_raw_string) .trim
            } (token.type ::comment =) {
                "<!--"
                self.xml_id i :(vtd.token_to_raw_string)
                "-->"
                .A""%
            } (token.type ::pi_name =) {
                "<?"
                self.xml_id i :(vtd.token_to_raw_string)
                .A""%
            } (token.type ::pi_val =) {
                self.xml_id i :(vtd.token_to_raw_string) :& E 0 = {;}{" "\}.?
                "?>"
                .A""%
            } (token.type ::cdata_val =) {
                "<![CDATA["
                self.xml_id i :(vtd.token_to_raw_string)
                "]]>"
                .A""%
            } (token.type ::dtd_val =) {
                "<!DOCTYPE"
                self.xml_id i :(vtd.token_to_raw_string)
                ">"
                .A""%
            }
        }:?
    } O "" % .# partial result on stack...

    .# close any unclosed tags
    [
        while {unclosed_tags E 0 >} {
            xml_tag_open {
                0 :xml_tag_open;
                "/>"
                unclosed_tags.pop;
            }{
                "</" unclosed_tags.pop ">"
            }.?
        }
    ] "" % +
}